
# This is WebDrove's core layer for site styles. It produces the overall page
# layout but then delegates to an appropriate page style to render the page
# content.

layerinfo "type" = "core";
layerinfo "name" = "WebDrove Main Core";
layerinfo "uniq" = "http://www.webdrove.org/ns/s2layers/site/core";

class Link {
    var string caption;
    var string url;
    var bool active;
    var bool current;
}

class Page {
    var string site_title;
    var string page_title;
    var Link[] nav;

    function print() "Entry point function; called by the system to render a page.";
    function builtin print_body() "Render the body content of the page.";
    function builtin print_head() "Produce HTML HEAD content necessary for this page.";
    function print_footer() "Produce some HTML to go in the footer area of the page.";
    function builtin external_props(string schema) : string{} "Return a set of externally-defined properties conforming to some schema.";
}

function builtin resource_url_impl(string fn) : string; # FIXME: Do this better
function resource_url(string fn) : string {
	return resource_url_impl($fn);
}

property string text_navigation {
	noui = 1;
}
set text_navigation = "Navigation";

function print_stylesheet() {
    # Does nothing in core
}

function Page::print() {
"""
<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Strict//EN" "http://www.w3.org/TR/xhtml1/DTD/xhtml1-strict.dtd">
<html>
<head>
<title>"""+ehtml($.site_title)+" - "+ehtml($.page_title)+"""</title>""";
	$this->print_head();
"""
</head>
<body>
<div id="pagecontainer">

<div id="top">
<div id="sitetitle">"""+ehtml($.site_title)+"""</div>
<div id="pagetitle">"""+ehtml($.page_title)+"""</div>

</div>

<div id="middle">

<div id="main">

<h1 id="pageheader">"""+ehtml($.page_title)+"""</h1>
<div id="page">
<div id="pagecontent">
<div id="pageinner">

""";

	$this->print_body();

"""

</div>
</div>
</div>

<div id="pageend"></div>

</div>

<div id="sidebar">
<div id="navigation">
<div id="navbar">

<h2>"""+ehtml($*text_navigation)+"""</h2>

<ul class="toplevellinks">""";

	# IE does stupid stuff if you have any whitespace in a list, so make sure not to introduce any here...
	foreach var Link l ($.nav) {
		var string cla = ($l.active ? "active" : "")+($l.active and $l.current ? " " : "")+($l.current ? "current" : "");
		print "<li class=\"$cla\"><a href=\""+ehtml($l.url)+"\" class=\"$cla\">"+ehtml($l.caption)+"</a></li>";
	}

"""</ul>

</div>
</div>
</div>
<div id="middleend"></div>
</div>

<div id="bottom">
""";

	$this->print_footer();

"""
</div>

</div>
</body>
</html>

""";

}

function Page::print_footer() {
	# Does nothing in core. Layouts can override.
}

function main_stylesheet() {
	# Does absolutely nothing in core
}
