
layerinfo "type" = "core";
layerinfo "name" = "Gallery Page Core";
layerinfo "uniq" = "http://www.webdrove.org/ns/s2layers/page/gallery";

class Image {
    var string url;
    var int width;
    var int height;
    var int id;
    var string title;
    var string description;
    
}

class Page {
    var string content "The page content as HTML.";

    function print() "Generate the page content; called by the system when a page needs to be rendered.";
}

class ThumbsPage extends Page 
{
	var Image[] images "The images to display on this page.";
}

property int thumbs_per_page {
}

set thumbs_per_page = 4;


class ImagePage extends Page
{
	var Image image "An image to display on this page.";
}

function ThumbsPage::print() {
    print $.content;

    """<table style="width:100%;" class="thumbslist"><tr>""";
	var int row = 1;
    foreach var Image i ($.images) {
	    """<td style="vertical-align : top;">""";
	    if ($i.url != "") {
	    """<div class="thumbimage">""";
		    "<a href='"+ehtml(string($i.id))+"/'><img src='"+ehtml($i.url)+"' width='"+ehtml(string($i.width))+"' height='"+ehtml(string($i.height))+"'  style='border : 0' /></a>";
		"</div>";
		}
	   """<div class="thumbinfo">""";
	    if ($i.title) {
		    "<h2 style='font-size:1.1em;font-weight:bold;'>"+ehtml($i.title)+"</h2>";
	    }
	
	    print $i.description;
	    "</div></td>";
		if($row == $*thumbs_per_page)
			{
				$row = 1;
				"</tr><tr>";
			}
			else
			{
				$row++;
			}
	    
    }
    if($row == 1)
    {
	"</tr>";
    }
    

    """</table>""";
}


function ImagePage::print() {
    print $.content;
    """<table style="width:100%;" class="imagetable">""";

	var Image i = $.image;
	    "<tr><td>";

	    if ($i.url != "") {
		    "<img src='"+ehtml($i.url)+"' width='"+ehtml(string($i.width))+"' height='"+ehtml(string($i.height))+"' style='border : 0' />";
		}
		"</td>";
	   "</tr><tr>";
	    "<td>";

	    if ($i.title) {
		    "<h2 style='font-size:1.1em;font-weight:bold;'>"+ehtml($i.title)+"</h2>";
	    }

	    print $i.description+"</td></tr>";

    """</table>""";
    """<p><a href="../">Back to thumbnails</a>""";
}
